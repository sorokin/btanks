import os, sys
Import('env')
Import('debug')
Import('sdl_cflags')
Import('sdl_libs')
Import('lib_dir')

env = env.Clone()
env.Append(CPPPATH=['..', '.'])
env.Append(LIBPATH=['../mrt'])
env.Append(LIBPATH=['.'])
env.Append(CPPDEFINES=['CLUNKAPI=DLLEXPORT'])

clunk_libs = ['SDL', 'mrt']

if sys.platform != 'win32':
	env.Append(LINKFLAGS=['-Wl,-rpath,'+ lib_dir])
	env.Append(LINKFLAGS=['-Wl,-rpath-link,build/' + env['mode'] + '/mrt'])
	env.Append(LINKFLAGS=['-Wl,-rpath-link,build/' + env['mode'] + '/sdlx'])
	clunk_libs.append('m')

env.MergeFlags(sdl_cflags, sdl_libs)

clunk = env.SharedLibrary('clunk', 
	[
		'context.cpp', 'sample.cpp', 'object.cpp', 'source.cpp', 'sdl_ex.cpp', 'stream.cpp', 
		'kemar.c', 'buffer.cpp', 'distance_model.cpp',
		'kiss/kiss_fftr.c', 'kiss/kiss_fft.c', 
	], 
	LIBS=clunk_libs)

if sys.platform != 'win32' and len(env['prefix']) > 0:
	Import('install_targets')
	Import('lib_dir')
	install_targets.append(Install(lib_dir, clunk))
else:
	Install('#', clunk[0])
