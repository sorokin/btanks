import sys
Import('env')
Import('sdl_cflags')
Import('sdl_libs')
Import('lib_dir')

env = env.Clone()
buildmode = env['mode']

libs = ['bt', 'sdlx', 'mrt']
env.Prepend(LIBPATH=['#/build/' + buildmode + '/engine'])
env.Prepend(LIBPATH=['#/build/' + buildmode + '/mrt'])
env.Prepend(LIBPATH=['#/build/' + buildmode + '/sdlx'])
env.Append(CPPPATH=['#/engine', '#/engine/src'])
env.MergeFlags(sdl_cflags, sdl_libs)

if sys.platform != 'win32':
	env.Append(LINKFLAGS=['-Wl,-rpath,'+ lib_dir])
	env.Append(LINKFLAGS=['-Wl,-rpath-link,build/' + env['mode'] + '/mrt'])
	env.Append(LINKFLAGS=['-Wl,-rpath-link,build/' + env['mode'] + '/sdlx'])
	env.Append(LINKFLAGS=['-Wl,-rpath-link,build/' + env['mode'] + '/clunk'])

sources = [
	'bullet.cpp', 'explosion.cpp', 'single_pose.cpp',
	'tank.cpp', 'shilka.cpp', 'launcher.cpp', 'ai_tank.cpp',
	'ai_launcher.cpp', 'ai_shilka.cpp', 'ai_trooper.cpp', 'ai_machinegunner_player.cpp', 
	'ai_heli.cpp', 	'ai_mortar.cpp', 'raider_heli.cpp', 
	'traffic_lights.cpp', 'missiles_in_vehicle.cpp', 'missile.cpp',
	'corpse.cpp', 'item.cpp', 'mine.cpp', 'dirt.cpp', 
	'damage.cpp', 'helicopter.cpp', 'paratrooper.cpp', 'kamikaze.cpp',
	'machinegunner_on_launcher.cpp', 'destructable_object.cpp', 'submarine.cpp', 'train.cpp',
	'trooper.cpp', 'fakemod.cpp', 'car.cpp', 'tooltip.cpp', 
	'barrack.cpp', 'watchtower.cpp', 'slime.cpp', 
	'cannon.cpp', 'boat.cpp', 'poison.cpp', 'old_school_destructable.cpp', 
	'zombie.cpp', 'civilian.cpp', 'teleport.cpp', 'cow.cpp', 
	'heli.cpp', 'bomb.cpp', 'explosive.cpp', 'mortar.cpp',
	'sandworm.cpp', 'barrier.cpp', 'pillbox.cpp', 'buggy.cpp', 'turrel.cpp', 
	'ballistic_missile.cpp', 'mortar_bullet.cpp', 'ctf_base.cpp', 'ctf_flag.cpp', 
	'shilka_turret.cpp', 
]

bt_obj = env.SharedLibrary('bt_objects', sources, LIBS=libs)
if sys.platform != 'win32' and len(env['prefix']) > 0:
	Import('install_targets')
	Import('plugins_dir')
	install_targets.append(Install(plugins_dir, bt_obj))
else:
	Install('#', bt_obj[0])
